plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
    id "com.vanniktech.maven.publish" version "0.27.0"
}

ext {
    mGroupId = "info.appdevnext"
    mArtifactId = "chart"
    mVersionName = getTag()

    mLibraryName = "Android Chart"
    mLibraryDescription = "A powerful Android chart view / graph view library, supporting line- bar- pie- radar- bubble- and candlestick charts as well as scaling, dragging and animations"
}

android {
    defaultConfig {
        minSdkVersion 19
        compileSdk 34
        targetSdkVersion 34

        // VERSION_NAME no longer available as of 4.1
        // https://issuetracker.google.com/issues/158695880
        buildConfigField 'String', 'VERSION_NAME', "\"${getTag()}\""

        consumerProguardFiles 'proguard-project.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true // this prevents "not mocked" error
    }
    namespace 'com.github.mikephil.charting'
}

dependencies {
    implementation 'androidx.annotation:annotation:1.7.1'
    testImplementation 'junit:junit:4.13.2'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

group = "info.mxtracks"
version = "${getTag()}"
println "Build version $version"

mavenPublishing {
    pom {
        name = "Android Chart"
        description = "A powerful Android chart view / graph view library, supporting line- bar- pie- radar- bubble- and candlestick charts as well as scaling, dragging and animations"
        inceptionYear = "2022"
        url = "https://github.com/AppDevNext/AndroidChart/"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "AppDevNext"
                name = "AppDevNext"
                url = "https://github.com/AppDevNext/"
            }
        }
        scm {
            url = "https://github.com/AppDevNext/AndroidChart/"
            connection = "scm:git:git://github.com/AppDevNext/AndroidChart.git"
            developerConnection = "scm:git:ssh://git@github.com/AppDevNext/AndroidChart.git"
        }
    }

    // Github packages
    repositories {
        maven {
            version = version+"-SNAPSHOT"
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/AppDevNext/AndroidChart"
            credentials {
                username = System.getenv("GITHUBACTOR")
                password = System.getenv("GITHUBTOKEN")
            }
        }
    }
}
