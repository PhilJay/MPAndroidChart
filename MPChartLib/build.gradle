plugins {
    id 'com.android.library'
    id 'maven-publish'
}

ext {
    mGroupId = "info.appdevnext"
    mArtifactId = "chart"
    mVersionName = getTag()

    mLibraryName = "Android Chart"
    mLibraryDescription = "A powerful Android chart view / graph view library, supporting line- bar- pie- radar- bubble- and candlestick charts as well as scaling, dragging and animations"
}

android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 33

        // VERSION_NAME no longer available as of 4.1
        // https://issuetracker.google.com/issues/158695880
        buildConfigField 'String', 'VERSION_NAME', "\"${getTag()}\""

        consumerProguardFiles 'proguard-project.txt'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true // this prevents "not mocked" error
    }
    namespace 'com.github.mikephil.charting'
}

dependencies {
    implementation 'androidx.annotation:annotation:1.5.0'
    testImplementation 'junit:junit:4.13.2'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

project.afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId mGroupId
                artifactId mArtifactId
                version mVersionName

                from components.release

//                artifact androidSourcesJar
//                artifact("$buildDir/outputs/aar/MPChartLib-release.aar")

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }

        repositories {
            maven {
                version = getTag()
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/AppDevNext/AndroidChart"
                credentials {
                    username = System.getenv("GITHUBACTOR")
                    password = System.getenv("GITHUBTOKEN")
                }
            }
        }
    }
}
